varying	vec3			position;

uniform	sampler2D		diffuse, normal, specular, depth;
uniform	mat4			inv_view;

const float near_plane = 0.1;
const float far_plane = 16384.0;
const float log_plane_ratio = log(far_plane / near_plane) - 1.0;
const float inv_256 = 1.0 / 256;
const float inv_255 = 1.0 / 255;

float color_to_distance(vec3 color)
{
	int tot = int(color.z * 255);
	tot *= 256;
	tot += int(color.y * 255);
	tot *= 256;
	tot += int(color.x * 255);

	float frac = float(tot) * inv_256 * inv_256 * inv_255;

	return exp(frac * log_plane_ratio + 1.0) * near_plane;
}

vec3 cameraVecToWorld(vec3 v) { return (inv_view * vec4(v, 0.0)).xyz; }
vec3 cameraPosToWorld(vec3 v) { return (inv_view * vec4(v, 1.0)).xyz; }

void main()
{
	vec2 uv = gl_TexCoord[0].xy;

	vec4 normal_sample = texture2D(normal, uv);
	vec4 diffuse_sample = texture2D(diffuse, uv);
	vec4 specular_sample = texture2D(specular, uv);
	vec4 depth_sample = texture2D(depth, uv);

	float z = color_to_distance(depth_sample.xyz);
	vec3 uPos = normalize(vec3(position.xy * z, -z) - cameraPosToWorld(vec3(0.0)));

	vec3 uNorm = normal_sample.xyz * 2.0 - 1.0;

	vec3 uLight = normalize(gl_LightSource[0].position.xyz);

	float light_dot = dot(uNorm, uLight);
	float eye_dot = dot(uNorm, uPos);
	vec3 uBounce = uPos - (2.0 * eye_dot) * uNorm;

	float diffuse_amount = max(0.0, min(1.0, light_dot));

	float specular_dot = dot(uBounce, uLight);
	float specular_base = max(0.0, min(1.0, specular_dot));
	float specular_amount = pow(specular_base, 4.0);

	vec3 diffuse_color = diffuse_sample.xyz;
	vec3 specular_color = specular_sample.xyz;

	vec3 base_color = diffuse_color * diffuse_amount * gl_LightSource[0].diffuse.xyz + specular_color * specular_amount * gl_LightSource[0].specular.xyz;

	gl_FragColor.xyz = base_color;
	gl_FragColor.w = 1.0;
}